#!/usr/bin/perl
#
# Our web-application requires that we can load a series of HTML::Template
# input-files.  These are located beneath C<templates/pages/>
#
# To avoid duplicating layout details we generate the files `*.tmpl`
# via the layout.tmpl file, and the *.in files
#
#

use strict;
use warnings;

use HTML::Template;


#
#  Load the layout-template.
#
my @layout = read_file("./templates/layouts/layout.tmpl");


#
#  Now process the pages
#
foreach my $file ( sort( glob("templates/pages/*.in") ) )
{
    my $out = $file;
    $out =~ s/\.in$/.tmpl/g;

    print "Generating $out from $file\n";

    #
    #  Load the contents of the file
    #
    my @page = read_file($file);

    #
    #  Open the file for output
    #
    open( my $handle, ">", $out ) or
      die "Failed to open file for writing $out - $!";

    foreach my $line (@layout)
    {
        if ( $line =~ /##CONTENT##/ )
        {
            foreach my $line (@page)
            {
                print $handle $line;
            }
        }
        else
        {
            print $handle $line;
        }
    }

    close($handle);
}


sub read_file
{
    my ($file) = (@_);

    my @ret;

    open( my $handle, "<", $file ) or die "Failed to open $file - $!";
    while ( my $line = <$handle> )
    {
        push( @ret, $line );
    }
    close($handle);

    return (@ret);
}
